// Copyright (C) 2017 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
apply from: "${buildscript.sourceFile.parentFile}/constants.gradle"
apply from: "${buildscript.sourceFile.parentFile}/javadoc_util.gradle"

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name != "release") {
        return // Skip non-release builds.
    }
    def allSourceDirs = variant.sourceSets.inject ([]) {
        acc, val -> acc << val.javaDirectories
    }
    task("generateJavadoc", type: Javadoc) {
        description = "Generates Javadoc for the ${javadocTitle}."
        title = "ExoPlayer ${javadocTitle}"
        source = allSourceDirs + "${buildDir}/generated/aidl_source_output_dir/"
        options {
            links "https://developer.android.com/reference",
                  "https://guava.dev/releases/$project.ext.guavaVersion/api/docs"
            encoding = "UTF-8"
        }
        exclude "**/BuildConfig.java"
        exclude "**/R.java"
        doFirst {
            classpath =
                files(
                    variant.javaCompileProvider.get().classpath.files,
                    project.android.getBootClasspath())
        }
        doLast {
            copy {
                from "src/main/javadoc"
                into "$buildDir/docs/javadoc"
            }
            project.fixJavadoc()
        }
    }
}
